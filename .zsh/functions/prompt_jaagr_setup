#
# My zsh prompt
#

function prompt_jaagr_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  autoload -Uz add-zsh-hook

  # Add hook called before each command
  add-zsh-hook precmd prompt_jaagr_precmd

  # Set git-info parameters
  zstyle ":prezto:module:git:info" verbose "yes"
  zstyle ":prezto:module:git:info:dirty"  format " %B%F{red}±%f%%b"
  zstyle ":prezto:module:git:info:branch" format " %B%K{9}%F{white} %b %%b%f"
  zstyle ":prezto:module:git:info:keys"   format "rprompt" "%b%c%s%D"

  # Set editor-info parameters
  zstyle ":prezto:module:editor:info:keymap:alternate" format " ── NORMAL ──"

  # Define prompts
  PROMPT="
%F{8}%~%f
%(?.%F{8}─────%f.%F{red}─────%f) "
  RPROMPT='${git_info[rprompt]}${editor_info[keymap]}%(?..%F{red} %?%f)'
  SPROMPT='zsh: correct %F{magenta}%R%f to %F{green}%r%f [nyae]? '
}

function prompt_jaagr_precmd {
  if (( $+functions[git-info] )); then
    git-info
  fi

  # window & icon title
  print -Pn "\e]0;Terminal (%n@%m) %~\a"
}

prompt_jaagr_setup "$@"
